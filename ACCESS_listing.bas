10 REM *PHILLIP EATON*ACCESS*19/1/88 TO 29/4/88*
20 :
30 :
40 :
50 REM ***** PROGRAM STARTS HERE - HERE GOES! *****
60 :
70 DIM SS$(12,30)
80 EXEC ***** SET UP *****
90 EXEC ***** TITLE AND INSTRUCTIONS *****
100 EXEC ***** DISPLAY SCREEN LAYOUT *****
110 REPEAT
120 EXEC ***** INPUT A KEY OR COMMAND *****
130 REM ***** TAKE ACTION ON KEY *****
140 F%=0
150 IFKB$="{f1}"THENF%=1:EXEC ***** MOVE HIGHLIGHT ALONG *****
160 IFKB$="{f7}"THENF%=1:EXEC ***** SELECT OPTION *****
170 IFKB$="{f3}"THENF%=1:EXEC ***** HOME THE SHEET *****
180 IFKB$="{f5}"THENF%=1:EXEC ***** END THE SHEET *****
190 TEMP%=0
200 IFKB$="{f2}"ORKB$="{f4}"ORKB$="{f6}"ORKB$="{f8}"THENTEMP%=1
210 IFTEMP%=1THENTEMP%=0:F%=1:EXEC ***** BIG UP DOWN LEFT RIGHT *****
220 IFKB$="{up}"ORKB$="{down}"ORKB$="{left}"ORKB$="{right}"THENTEMP%=2
230 IFTEMP%=2THENTEMP%=0:F%=1:EXEC ***** CURSOR KEYS MOVEMENT *****
240 IFKB$=CHR$(13)THENF%=1:EXEC ***** ENTER ENTRY$ INTO SPREAD SHEET *****
250 IFKB$=CHR$(20)THENF%=1:EXEC ***** DELETE 1 CHAR FROM ENTRY$ *****
260 IFF%=0 THEN EXEC ***** ADD 1 CHAR TO ENTRY$ *****
270 UNTIL QUIT%=1
280 STOP
290 :
300 :
310 PROC ***** SET UP ******
320 COLOUR0,0:POKE646,13
330 KEY1,CHR$(133):KEY2,CHR$(137):KEY3,CHR$(134):KEY4,CHR$(138):KEY5,CHR$(135)
340 KEY6,CHR$(139):KEY7,CHR$(136):KEY8,CHR$(140)
350 KEY9,"S"+CHR$(34)+"@:ACCESS"+CHR$(34)+",8"+CHR$(13):REM CBM KEY+F1
360 HI%=1:VOL15
370 QUIT%=0
380 XX%=1:YY%=1:YE%=1:XE%=1:CX%=1:CY%=1:EP%=0:EN$="":DP%=6
390 IFTEMP%<>1THENPRINTCHR$(147)
400 EXEC ***** FILL CELLS *****
410 END PROC *****
420 :
430 :
440 :
450 PROC ***** FILL CELLS *****
460 PRINTAT(1,2)"PLEASE WAIT - FILLING CELLS"
470 FOR II=1TO 12:FORJJ=1TO20:SS$(II,JJ)="":NEXTJJ,II
480 END PROC *****
490 :
500 :
510 :
520 :
530 :
540 PROC ***** TITLE AND INSTRUCTIONS *****
550 PRINT CHR$(147)
560 HIRES13,0
570 TEXT37,20,"AACCESS",1,4,16
580 REC020,04,120,61,1:REC16,00,128,69,1
590 REC021,04,118,61,1:REC17,00,128,69,1
600 XC=186
610 TEXTXC,000,"AA",1,2,12
620 TEXTXC,018,"ACBOMPUTER",1,2,12
630 TEXTXC,036,"ACBONTROLLED",1,2,12
640 TEXTXC,054,"AEBLECTRONIC",1,2,12
650 TEXTXC,072,"ASBPREAD",1,2,12
660 TEXTXC,090,"ASBHEET",1,2,12
670 TEXT19,87,"A(C) PBHILLIP AEBATON",1,1,7
680 TEXT 52,162,"AÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ",1,1,8
690 TEXT 52,170,"A{reverse on}{SPACE*2}INSTRUCTIONS (Y OR N) ?{SPACE*2}",1,2,8
700 REPEAT:GET A$:UNTILA$="Y"ORA$="N"
710 NRM
720 IF A$="Y"THEN EXEC ***** DISPLAY INSTRUCTIONS *****
730 END PROC *****
740 :
750 :
760 PROC ***** DISPLAY INSTRUCTIONS *****
770 PRINT CHR$(147);
771 PRINTAT(2,1)"F1 MOVES HIGHLIGHT ALONG"
772 PRINTAT(2,2)"F7 SELECTS OPTION"
773 PRINTAT(2,3)"F3 HOME THE SHEET"
774 PRINTAT(2,4)"F5 GOTO END OF SHEET"
775 PRINTAT(2,5)"F2/4/6/8 BIG UP/DOWN/LEFT/RIGHT"
776 PRINTAT(2,6)"CURSOR KEYS AS IN BASIC"
777 PRINTAT(2,7)"@ SIGNIFYS FORMULA"
778 PRINTAT(2,8)"RETURN TO ENTER ENTRY"
790 PRINTAT(09,23)"PRESS <F7> TO CONTINUE"
800 GET A$:IFA$<>"{f7}"THEN960
810 END PROC *****
820 :
830 :
840 PROC ***** DISPLAY SCREEN LAYOUT *****
850 PRINTCHR$(147):POKE198,0
860 PRINTAT(0,1)"Ú{down}{left}³{down}{left}À{down}{left}ENTRY:"AT(39,1)"¿{down}{left}³{down}{left}Ù{down}{LEFT*4}:---";
870 EXEC ***** PRINT LETTERS ACROSS THE TOP *****
880 EXEC ***** PRINT NUMBERS DOWN THE SIDE *****
890 PRINTAT(0,0)"COPY FILE GOTO CALC HARD HELP QUIT FORM"
900 INV0,(HI%-1)*5,4,1
910 EXEC ***** DISPLAY CELLS ON SCREEN *****
920 END PROC *****
930 :
940 :
950 :
960 PROC ***** INPUT A KEY OR COMMAND *****
970 REPEAT: GETKB$:UNTILKB$<>""
1000 POKE198,0
1010 END PROC *****
1020 :
1030 :
1040 :
1050 :
1060 :
1070 :
1080 PROC ***** MOVE HIGHLIGHT ALONG *****
1090 INV0,(HI%-1)*5,4,1
1100 HI%=HI%+1:IFHI%=9THENHI%=1
1110 INV0,(HI%-1)*5,4,1
1120 END PROC *****
1130 :
1140 :
1150 :
1160 :
1170 PROC ***** DISPLAY CELLS ON SCREEN *****
1180 EXEC ***** CLEAR THE COMMENT BOX *****
1190 EXEC ***** PRINT 'WORKING' MESSAGE *****
1200 FORI=XX%TOXX%+5:FORJ=YY%TOYY%+17
1210 TEMP%=0
1220 IF LEFT$(SS$(I,J),1)="@"THENEXEC ***** GET VALUE FROM FORMULA *****
1230 IFLEFT$(SS$(I,J),1)<>"@"THENTEMP$=SS$(I,J)
1240 IF TEMP$=""THENTEMP$="{SPACE*6}"
1250 PRINT AT(3+((I-XX%+1-1)*6),5+J-YY%+1)TEMP$
1260 NEXT J,I
1270 EXEC ***** CLEAR THE COMMENT BOX *****
1280 INV 5+CY%,3+(CX%-1)*6,6,1
1290 EXEC ***** CLEAR EN$ ON SCREEN *****
1300 PRINTAT(6,4)SS$(XX%+CX%-1,YY%+CY%-1)
1310 EN$=SS$(XX%+CX%-1,YY%+CY%-1):EP%=LEN(EN$)
1320 END PROC *****
1330 :
1340 :
1350 :
1360 PROC ***** GET VALUE FROM FORMULA *****
1370 TEMP%=1:TEMP$=""
1380 REPEAT
1390 TEMP%=TEMP%+1
1400 TEMP$=MID$(SS$(I,J),TEMP%,1)
1410 UNTILTEMP$="@"ORTEMP%=LEN(SS$(I,J))
1420 IFTEMP$="@"THENTEMP$=RIGHT$(SS$(I,J),LEN(SS$(I,J))-TEMP%):ELSE:TEMP$="?FORM?"
1430 REPEAT
1440 IFLEN(TEMP$)<6THENTEMP$=TEMP$+" "
1450 UNTIL LEN(TEMP$)>=6:TEMP$=LEFT$(TEMP$,DP%)
1460 REPEAT:TEMP$=TEMP$+" ":UNTILLEN(TEMP$)=7:TEMP$=LEFT$(TEMP$,6)
1470 END PROC *****
1480 :
1490 :
1500 :
1510 PROC ***** HOME THE SHEET *****
1520 XX%=1:YY%=1:X%=1:Y%=1:CX%=1:CY%=1
1530 EXEC ***** PRINT 'WORKING' MESSAGE *****
1540 EXEC ***** PRINT NUMBERS DOWN THE SIDE *****
1550 EXEC ***** PRINT LETTERS ACROSS THE TOP *****
1560 EXEC ***** DISPLAY CELLS ON SCREEN *****
1570 END PROC *****
1580 :
1590 :
1600 :
1610 PROC ***** CLEAR EN$ ON SCREEN *****
1620 PRINT AT(6,4)"{SPACE*30}"
1630 END PROC *****
1640 :
1650 :
1660 :
1670 PROC ***** CLEAR THE COMMENT BOX *****
1680 FORMN=1TO3:PRINT AT(1,MN)"{SPACE*38}":NEXTMN
1690 END PROC *****
1700 :
1710 :
1720 :
1730 :
1740 PROC ***** CURSOR KEYS MOVEMENT *****
1750 TEMP%=0
1760 IF KB$="{down}"ANDYY%=79ANDCY%=18THEN TEMP%=1:EXEC ***** BOUNDRY ERROR *****
1770 IF KB$="{up}"ANDYY%=1 ANDCY%=1 THEN TEMP%=1:EXEC ***** BOUNDRY ERROR *****
1780 IF KB$="{left}"ANDXX%=1 ANDCX%=1 THEN TEMP%=1:EXEC ***** BOUNDRY ERROR *****
1790 IF KB$="{right}"ANDXX%=7 ANDCX%=6 THEN TEMP%=1:EXEC ***** BOUNDRY ERROR *****
1800 IF TEMP%=0 THEN EXEC ***** MOVEMENT *****
1810 EP%=0:EN$=""
1820 EXEC ***** CLEAR EN$ ON SCREEN *****
1830 PRINT AT(6,4)SS$(XX%+CX%-1,YY%+CY%-1)
1840 END PROC *****
1850 :
1860 :
1870 :
1880 PROC ***** BOUNDRY ERROR *****
1890 EXEC ***** CLEAR THE COMMENT BOX *****
1900 PRINT AT(10,1)"***** BOUNDRY ERROR *****"
1910 PRINT AT(2,2)"YOU CANNOT MOVE OFF THE SPREAD SHEET"
1920 EXEC ***** BEEP *****
1930 FORI=1TO1200:NEXTI
1940 EXEC ***** CLEAR THE COMMENT BOX *****
1950 END PROC *****
1960 :
1970 :
1980 :
1990 PROC ***** MOVEMENT *****
2000 IFKB$="{right}"ANDCX%=6THENXX%=XX%+3:TEMP%=1
2010 IF TEMP%=1THEN EXEC ***** PRINT LETTERS ACROSS THE TOP *****
2020 IF TEMP%=1THENTEMP%=0:CX%=CX%-3:EXEC ***** DISPLAY CELLS ON SCREEN *****
2030 IFKB$="{right}"THENEXEC ***** INVERSE *****
2040 IFKB$="{right}"THENCX%=CX%+1:EXEC ***** INVERSE *****
2050 :
2060 IFKB$="{left}"ANDCX%=1THENXX%=XX%-3:TEMP%=1
2070 IF TEMP%=1THEN EXEC ***** PRINT LETTERS ACROSS THE TOP *****
2080 IF TEMP%=1THENTEMP%=0:CX%=CX%+3:EXEC ***** DISPLAY CELLS ON SCREEN *****
2090 IFKB$="{left}"THENEXEC ***** INVERSE *****
2100 IFKB$="{left}"THENCX%=CX%-1:EXEC ***** INVERSE *****
2110 :
2120 IFKB$="{down}"ANDCY%=18THENYY%=YY%+6:TEMP%=1
2130 IFTEMP%=1THEN EXEC ***** PRINT NUMBERS DOWN THE SIDE *****
2140 IF TEMP%=1THENTEMP%=0:CY%=CY%-6:EXEC ***** DISPLAY CELLS ON SCREEN *****
2150 IF KB$="{down}"THEN EXEC ***** INVERSE *****
2160 IF KB$="{down}"THENCY%=CY%+1:EXEC ***** INVERSE *****
2170 :
2180 IFKB$="{up}"ANDCY%=1THENYY%=YY%-6:TEMP%=1
2190 IFTEMP%=1THENEXEC ***** PRINT NUMBERS DOWN THE SIDE *****
2200 IF TEMP%=1THENTEMP%=0:CY%=CY%+6:EXEC ***** DISPLAY CELLS ON SCREEN *****
2210 IF KB$="{up}"THENEXEC ***** INVERSE *****
2220 IF KB$="{up}"THEN CY%=CY%-1:EXEC ***** INVERSE *****
2230 :
2240 END PROC *****
2250 :
2260 :
2270 :
2280 PROC ***** INVERSE *****
2290 INV5+CY%,3+(CX%-1)*6,6,1
2300 END PROC *****
2310 :
2320 :
2330 :
2340 PROC ***** PRINT NUMBERS DOWN THE SIDE *****
2350 FOR I=1TO18:PRINTAT(0,5+I)"{reverse on}{SPACE*2}"AT(3-LEN(STR$(YY%+I-1)),5+I)"{reverse on}";YY%+I-1:NEXT
2360 PRINT"{reverse off}";
2370 END PROC *****
2380 :
2390 :
2400 :
2410 PROC ***** PRINT LETTERS ACROSS THE TOP *****
2420 FORI=0TO5:PRINTAT(3+I*6,5)"{reverse on}{SPACE*2}";CHR$(I+65+XX%-1);"{SPACE*2}³{reverse off}";:NEXTI
2430 END PROC *****
2440 :
2450 :
2460 :
2470 PROC ***** PRINT 'WORKING' MESSAGE *****
2480 FORMN=1TO3:PRINT AT(1,MN)"{SPACE*38}":NEXTMN
2490 PRINTAT(15,2)"WORKING...";AT(10,3)"PLEASE WAIT A SHORT WHILE"
2500 END PROC *****
2510 :
2520 :
2530 :
2540 PROC ***** BIG UP DOWN LEFT RIGHT *****
2550 EXEC ***** PRINT 'WORKING' MESSAGE *****
2560 REM U D L R
2570 TEMP%=0
2580 IF KB$="{f2}"AND YY%>18THENYY%=YY%-18:TEMP%=1
2590 IF KB$="{f4}"AND YY%<62THENYY%=YY%+18:TEMP%=1
2600 IF KB$="{f6}"AND XX%>6THENXX%=XX%-6:TEMP%=1
2610 IF KB$="{f8}"AND XX%<2THENXX%=XX%+6:TEMP%=1
2620 IF TEMP%=0THEN EXEC ***** CLEAR THE COMMENT BOX *****
2630 IF TEMP%=0 THEN PRINTAT(5,1)"THERE IS NOT ANOTHER FULL SCREEN"
2640 IFTEMP%=0THENPRINTAT(5,2)"TO PRINT IN THAT DIRECTION":EXEC ***** BEEP *****
2650 FOR I=1 TO 2000:NEXT:EXEC ***** CLEAR THE COMMENT BOX *****
2660 IF TEMP%=1THENEXEC ***** ADD ON *****
2670 END PROC *****
2680 :
2690 :
2700 :
2710 PROC ***** BEEP *****
2720 WAVE1,00010000:ENVELOPE1,1,10,1,0:MUSIC1,"{clear}1G5{F1*5}":PLAY1
2730 END PROC *****
2740 :
2750 :
2760 :
2770 PROC ***** ADD ON *****
2780 EXEC ***** PRINT NUMBERS DOWN THE SIDE *****
2790 EXEC ***** PRINT LETTERS ACROSS THE TOP *****
2800 EXEC ***** DISPLAY CELLS ON SCREEN *****
2810 END PROC *****
2820 :
2830 :
2840 :
2850 PROC ***** ADD 1 CHAR TO ENTRY$ *****
2860 IF EP%=>30THENPRINTAT(3,2)"NO MORE THAN 30 CHARS IN EACH ENTRY"
2870 IF EP%=>30 THENEXEC ***** BEEP *****
2880 IF EP%=>30 THENFORI=1TO1200:NEXT:EXEC ***** CLEAR THE COMMENT BOX *****
2890 IF EP%=0THEN EXEC ***** CLEAR EN$ ON SCREEN *****
2900 IF EP%<30 THENEN$=EN$+KB$:EP%=EP%+1:PRINTAT(6,4)EN$
2910 END PROC *****
2920 :
2930 :
2940 :
2950 :
2960 PROC ***** DELETE 1 CHAR FROM ENTRY$ *****
2970 IF EP%>0THENEP%=EP%-1:EN$=LEFT$(EN$,EP%):PRINTAT(6,4)EN$;" "
2980 END PROC *****
2990 :
3000 :
3010 :
3020 PROC ***** ADD TO ENTRY$ SO DIVISIBLE BY SIX *****
3030 IF EN$=""THENEN$=SS$(XX%+CX%-1,YY%+CY%-1):EP%=6
3040 TEMP%=0:REPEAT
3050 IF EN$=""THENEN$="{SPACE*6}":EP%=6
3060 IF LEN(EN$)/6<>INT((LEN(EN$))/6)THENEN$=EN$+" ":ELSE:TEMP%=1
3070 UNTIL TEMP%=1
3080 END PROC *****
3090 :
3100 :
3110 :
3120 PROC ***** ENTER ENTRY$ INTO SPREAD SHEET *****
3130 IF LEFT$(EN$,1)<>"@"THEN EXEC ***** ADD TO ENTRY$ SO DIVISIBLE BY SIX *****
3140 IF LEFT$(EN$,1)<>"@"THEN EXEC ***** PUT ENTRY$ INTO SHEET IN SIXES *****
3150 IF LEFT$(EN$,1)="@"THEN EXEC ***** PUT FORMULA INTO SHEET *****
3160 END PROC *****
3170 :
3180 :
3190 :
3200 PROC ***** PUT FORMULA INTO SHEET *****
3210 SS$(XX%+CX%-1,YY%+CY%-1)=EN$
3220 PRINT AT(2+(CX%-1)*6 +XX%,5+CY%-1+YY%)"*FORM*"
3230 IF CY%-1+YY%>YE%THENYE%=CY%-1+YY%
3240 IF CX%-1+XX%>XE%THEN XE%=CX%-1+XX%
3250 EXEC ***** INVERSE *****
3260 END PROC *****
3270 :
3280 :
3290 PROC ***** PUT ENTRY$ INTO SHEET IN SIXES *****
3300 I%=0:FI%=0
3310 REPEAT
3320 IFCX%-1+XX%+I%>12THENFI%=1
3330 IFFI%=0 THEN SS$(I%+CX%+XX%-1,CY%+YY%-1)=MID$(EN$,1+6*I%,6)
3340 IF CX%-1+XX%+I%>XE%THEN XE%=I%+CX%-1+XX%
3350 IF FI%=0 AND CX%+I%<7THEN II=1
3360 IFII=1THEN II=0:PRINT AT(3+(CX%-1+I%)*6,5+CY%)SS$(I%+XX%+CX%-1,YY%+CY%-1)
3370 I%=I%+1
3380 UNTIL I%=LEN(EN$)/6
3390 IF CY%-1+YY%>YE%THENYE%=CY%-1+YY%
3400 EXEC ***** CLEAR EN$ ON SCREEN *****
3410 PRINT AT(6,4)SS$(XX%+CX%-1,YY%+CY%-1)
3420 EN$="":EP%=0
3430 EXEC ***** INVERSE *****
3440 END PROC *****
3450 :
3460 :
3470 :
3480 PROC ***** END THE SHEET *****
3490 TEMP%=0:I%=0
3500 TEMP%=YE%-1
3510 REPEAT
3520 TEMP%=TEMP%+1
3530 UNTIL TEMP%/6=INT(TEMP%/6) AND TEMP%>12
3540 I%=XE%-1
3550 REPEAT
3560 I%=I%+1
3570 UNTIL I%/3=INT(I%/3) ANDI%>3
3580 XX%=I%-5:YY%=TEMP%-17:CX%=XE%-XX%+1:CY%=YE%-YY%+1
3590 EXEC ***** PRINT 'WORKING' MESSAGE *****
3600 EXEC ***** PRINT NUMBERS DOWN THE SIDE *****
3610 EXEC ***** PRINT LETTERS ACROSS THE TOP *****
3620 EXEC ***** DISPLAY CELLS ON SCREEN *****
3630 END PROC *****
3640 :
3650 :
3660 :
3670 PROC ***** SELECT OPTION *****
3680 :
3690 IF HI%=1 THEN EXEC ***** COPY SELECTED *****
3700 IF HI%=2 THEN EXEC ***** FILE SELECTED *****
3710 IF HI%=3 THEN EXEC ***** GOTO SELECTED *****
3720 IF HI%=4 THEN EXEC ***** CALC SELECTED *****
3730 IF HI%=5 THEN EXEC ***** HARD SELECTED *****
3750 IF HI%=7 THEN EXEC ***** QUIT SELECTED *****
3760 IF HI%=8 THEN EXEC ***** FORM SELECTED *****
3765 IF HI%=6 THEN EXEC ***** HELP SELECTED *****
3770 END PROC*****
3780 :
3790 :
3800 :
3810 :
3820 PROC ***** QUIT SELECTED *****
3830 EXEC ***** CLEAR THE COMMENT BOX *****
3840 PRINT AT(2,2)"DO YOU WANT TO:-{SPACE*3}(Q)UIT THE SHEET"
3850 PRINT AT(2,1)"{SPACE*19}(C)LEAR THE SHEET"
3860 PRINT AT(2,3)"{SPACE*19}(N)ONE OF THESE"
3870 REPEAT
3880 EXEC ***** INPUT A KEY OR COMMAND *****
3890 UNTIL KB$="Q" OR KB$="N" OR KB$="C"
3900 EXEC ***** CLEAR THE COMMENT BOX *****
3910 IF KB$="Q"THEN EXEC ***** QUIT ROUTINE *****
3920 TEMP%=0:IF KB$="C"THEN EXEC ***** CLEAR SHEET *****
3930 IF TEMP%=1THEN EXEC ***** DISPLAY CELLS ON SCREEN *****
3940 END PROC *****
3950 :
3960 :
3970 :
3980 :
3990 PROC ***** QUIT ROUTINE *****
4000 PRINT AT(2,2)"CHECK YOUR SPREAD SHEET IS SAVED."
4010 PRINT AT(2,3)"ARE YOU SURE (Y)ES/(N)O ?"
4020 REPEAT
4030 EXEC ***** INPUT A KEY OR COMMAND *****
4040 UNTIL KB$="Y" OR KB$="N"
4050 EXEC ***** CLEAR THE COMMENT BOX *****
4060 IFKB$="Y"THENQUIT%=1
4070 KB$=""
4080 END PROC *****
4090 :
4100 :
4110 :
4120 PROC ***** CLEAR SHEET *****
4130 :
4140 INV0,(HI%-1)*5,4,1:TEMP%=1
4150 EXEC ***** SET UP *****
4160 EXEC ***** CLEAR EN$ ON SCREEN *****
4170 EXEC ***** PRINT LETTERS ACROSS THE TOP *****
4180 EXEC ***** PRINT NUMBERS DOWN THE SIDE *****
4190 INV0,(HI%-1)*5,4,1
4200 END PROC *****
4210 :
4220 :
4230 :
4240 :
4250 PROC ***** HARD SELECTED *****
4260 EXEC ***** CLEAR THE COMMENT BOX *****
4270 PRINT AT(2,1)"ENSURE PRINTER IS CORRECTLY ADJUSTED"
4280 PRINT AT(2,2)"AND SUFFICIENT PAPER IS AVAILIBLE"
4290 PRINT AT(2,3)"PRESS (A)BORT OR (C)ONTINUE"
4300 REPEAT
4310 EXEC ***** INPUT A KEY OR COMMAND *****
4320 UNTIL KB$="A" OR KB$="C"
4330 IF KB$="C"ANDXE%>0THEN EXEC ***** PRINT OUT *****
4340 IF KB$="C"ANDXE%=0THENEXEC ***** CLEAR THE COMMENT BOX *****
4350 IF KB$="C"ANDXE%=0THENPRINTAT(4,2)"YOU CAN'T, THERE IS NOTHING THERE"
4360 IF KB$="C"ANDXE%=0THEN EXEC ***** BEEP *****
4370 IF KB$="C"ANDXE%=0THEN EXEC ***** CLEAR THE COMMENT BOX *****
4380 KB$="":EXEC ***** CLEAR THE COMMENT BOX *****
4390 END PROC *****
4400 :
4410 :
4420 :
4430 PROC ***** PRINT OUT *****
4440 OPEN1,4
4450 I%=0:TEMP%=0:TEMP$="":KB$=""
4460 EXEC ***** CLEAR THE COMMENT BOX *****
4470 PRINTAT(2,2)"PRESS (S) TO STOP AT ANY TIME"
4480 REPEAT
4490 I%=I%+1
4500 TEMP%=0:TEMP$=""
4510 REPEAT
4520 TEMP%=TEMP%+1
4530 IFLEFT$(SS$(TEMP%,I%),1)="@"THENEXEC ***** PRINT FORMULA *****
4540 IF LEFT$(SS$(TEMP%,I%),1)<>"@"THENTEMP$=TEMP$+SS$(TEMP%,I%)
4550 IF SS$(TEMP%,I%)=""THENTEMP$=TEMP$+"{SPACE*6}"
4560 UNTIL TEMP% =XE%
4570 PRINT#1,TEMP$
4580 GET KB$
4590 UNTIL I%=YE% OR KB$="S"
4600 PRINT#1:PRINT#1:PRINT#1:PRINT#1:CLOSE1
4610 EXEC ***** BEEP *****
4620 EXEC ***** CLEAR THE COMMENT BOX *****
4630 END PROC *****
4640 :
4650 :
4660 :
4670 :
4680 PROC ***** PRINT FORMULA *****
4690 FI%=1:M$="":REPEAT:FI%=FI%+1:M$=MID$(SS$(TEMP%,I%),FI%,1)
4700 UNTILM$="@"ORFI%=LEN(SS$(TEMP%,I%))
4710 IFM$="@"THENM$=RIGHT$(SS$(TEMP%,I%),LEN(SS$(TEMP%,I%))-FI%):ELSE:M$="?FORM?"
4720 REPEAT:IFLEN(M$)<6THENM$=M$+" "
4730 UNTILLEN(M$)>=6:M$=LEFT$(M$,DP%)
4740 REPEAT:M$=M$+" ":UNTILLEN(M$)=7:M$=LEFT$(M$,6)
4750 TEMP$=TEMP$+M$
4755 END PROC *****
4760 :
4770 :
4780 PROC ***** FILE SELECTED *****
4790 NE%=0
4800 REPEAT
4810 EXEC ***** CLEAR THE COMMENT BOX *****
4820 PRINT AT(2,1)"DO YOU WANT TO:-{SPACE*6}(L)OAD A FILE"
4830 PRINT AT(2,2)"(A)BORT FILE OPTION{SPACE*3}(S)AVE A FILE"
4840 PRINT AT(2,3)"(V)IEW ERROR CHANNEL{SPACE*2}(E)RASE A FILE"
4850 KB$="":REPEAT
4860 EXEC ***** INPUT A KEY OR COMMAND *****
4870 UNTIL KB$="L"ORKB$="S"ORKB$="A"ORKB$="E"ORKB$="V"
4880 IF KB$="A"THEN NE%=1
4890 IF KB$="V"THEN EXEC ***** VIEW ERROR CHANNEL *****
4900 IF KB$="E"THEN EXEC ***** ERASE A FILE *****
4910 IF KB$="S"THEN EXEC ***** SAVE A FILE *****
4920 IF KB$="L"THEN EXEC ***** LOAD A FILE *****
4930 UNTIL NE%=1
4940 EXEC ***** CLEAR THE COMMENT BOX *****
4950 END PROC *****
4960 :
4970 :
4980 :
4990 :
5000 PROC ***** SAVE A FILE *****
5010 EXEC ***** CLEAR THE COMMENT BOX *****
5020 EXEC ***** INPUT FILE NAME *****
5030 IFLEFT$(FE$,3)<>"XXX"THEN EXEC ***** SAVE *****
5040 EXEC ***** CLEAR THE COMMENT BOX *****
5050 END PROC *****
5060 :
5070 :
5080 :
5090 PROC ***** SAVE *****
5100 FE$=""+LEFT$(FE$,15)
5110 EXEC ***** CLEAR THE COMMENT BOX *****
5120 PRINT AT(2,2)"SAVING...{SPACE*6}PLEASE WAIT"
5130 :
5140 OPEN1,8,2,"@0:"+FE$+",S,W"
5150 HEAD$=CHR$(XE%)+CHR$(YE%)+"{SPACE*4}"
5160 PRINT#1,HEAD$CHR$(13)
5170 FORI=1TOXE%:FORJ=1TOYE%
5180 EXEC ***** SWAP SPACES FOR BALLS *****
5190 PRINT#1,TEMP$CHR$(13);
5200 NEXTJ,I
5210 CLOSE1
5220 END PROC *****
5230 :
5240 :
5250 :
5260 :
5270 PROC ***** VIEW ERROR CHANNEL *****
5280 EXEC ***** CLEAR THE COMMENT BOX *****
5290 OPEN1,8,15
5300 INPUT#1,EC$,DE$
5310 PRINTAT(2,1)"DOS ERROR CODE :- ";EC$
5320 PRINTAT(2,2)"DESCRIPTION :- "DE$
5330 CLOSE1
5340 PRINTAT(2,3)"PRESS A KEY TO CONTINUE"
5350 REPEAT
5360 EXEC ***** INPUT A KEY OR COMMAND *****
5370 UNTIL KB$<>""
5380 EXEC ***** CLEAR THE COMMENT BOX *****
5390 END PROC *****
5400 :
5410 :
5420 :
5430 :
5440 PROC ***** INPUT FILE NAME *****
5450 PRINTAT(2,2)"(MAXIMUM OF 15 CHARS , XXX TO ABORT)"
5460 PRINTAT(12,1)"INPUT FILE NAME"AT(2,3)":-"
5470 TEMP%=0:FI%=0:FE$=""
5480 REPEAT
5490 REPEAT:GETKB$:UNTILKB$<>""
5500 IF ASC(KB$)>64 ANDASC(KB$)<91ANDTEMP%<15THEN FE$=FE$+KB$:TEMP%=TEMP%+1
5510 IF KB$=CHR$(20)ANDTEMP%>0THEN FE$=LEFT$(FE$,LEN(FE$)-1):TEMP%=TEMP%-1
5520 IFKB$=CHR$(32)ANDTEMP%<15THENFE$=FE$+KB$:TEMP%=TEMP%+1
5530 PRINTAT( 5,3)FE$"{SPACE*15}"
5540 IF KB$=CHR$(13)THEN FI%=1
5550 UNTIL FI%=1
5560 END PROC *****
5570 :
5580 :
5590 :
5600 PROC ***** ERASE A FILE *****
5610 EXEC ***** CLEAR THE COMMENT BOX *****
5620 EXEC ***** CHOOSE FILE FROM DISC DIRECTORY *****
5630 FE$=NAME$
5640 EXEC ***** CLEAR THE COMMENT BOX *****
5650 IF FE$<>""THEN PRINT AT(2,2)"ERASING...{SPACE*8}PLEASE WAIT"
5660 IF FE$<>""THEN OPEN1,8,15,"S0:"+FE$:CLOSE1
5670 EXEC ***** CLEAR THE COMMENT BOX *****
5680 END PROC *****
5690 :
5700 :
5710 :
5720 :
5730 PROC ***** CHOOSE FILE FROM DISC DIRECTORY *****
5740 OPEN1,8,0,"$0"
5750 GET#1,A$,A$,A$,A$
5760 REPEAT
5770 GET#1,A$,A$
5780 FI%=0
5790 REPEAT
5800 GET#1,B$
5810 IF ST<>0 THENFI%=1
5820 UNTIL B$=CHR$(34) OR FI%=1
5830 IF FI%=0 THEN EXEC ***** TAKE OFF DISC *****
5840 UNTIL FI%=1
5850 IFNAME$=""THENPRINTAT(6,3)"*** NO MORE LEFT ON DISC ***":FORI=1TO1000:NEXTI
5860 CLOSE1
5870 END PROC *****
5880 :
5890 :
5900 :
5910 :
5920 PROC ***** TAKE OFF DISC *****
5930 PRINTAT(1,2)"PLEASE (A)CCEPT OR (R)EJECT A FILENAME"
5940 PRINTAT(7,3)"----:"AT(27,3)":----"
5950 NAME$=""
5960 REPEAT
5970 GET#1,TEMP$
5980 NAME$=NAME$+TEMP$
5990 UNTIL TEMP$=CHR$(34)
6000 NAME$=LEFT$(NAME$,LEN(NAME$)-1)
6010 IF LEFT$(NAME$,1)=""THENPRINTAT(12,3)LEFT$(RIGHT$(NAME$,LEN(NAME$)-1),15)
6020 KB$="":IF LEFT$(NAME$,1)=""THEN REPEAT:GETKB$:UNTILKB$="A"ORKB$="R"
6030 PRINT AT(12,3)"{SPACE*15}"
6040 IFKB$="A"THENFI%=1:ELSE:NAME$=""
6050 REPEAT
6060 GET#1,TEMP$
6070 UNTIL TEMP$<>CHR$(32)
6080 REPEAT
6090 GET#1,TEMP$
6100 UNTIL TEMP$=""
6110 IF ST<>0 THENFI%=1
6120 END PROC *****
6130 :
6140 :
6150 :
6160 :
6170 PROC ***** LOAD A FILE *****
6180 EXEC ***** CLEAR THE COMMENT BOX *****
6190 EXEC ***** CHOOSE FILE FROM DISC DIRECTORY *****
6200 FE$=NAME$
6210 IF FE$<>""THEN EXEC ***** LOAD ****
6220 EXEC ***** CLEAR THE COMMENT BOX *****
6230 REM ***** HOME THE SHEET *****
6240 XX%=1:YY%=1:X%=1:Y%=1:CX%=1:CY%=1
6250 EXEC ***** PRINT 'WORKING' MESSAGE *****
6260 EXEC ***** PRINT NUMBERS DOWN THE SIDE *****
6270 EXEC ***** PRINT LETTERS ACROSS THE TOP *****
6280 EXEC ***** DISPLAY CELLS ON SCREEN *****
6290 END PROC *****
6300 :
6310 :
6320 :
6330 :
6340 PROC ***** LOAD *****
6350 NE%=1
6360 EXEC ***** CLEAR THE COMMENT BOX *****
6370 EXEC ***** FILL CELLS *****
6380 EXEC ***** CLEAR THE COMMENT BOX *****
6390 PRINT AT(2,2)"LOADING...{SPACE*7}PLEASE WAIT"
6400 OPEN1,8,2,"0:"+FE$+",S,R"
6410 INPUT#1,HEAD$
6420 XE%=ASC(MID$(HEAD$,1,1)):YE%=ASC(MID$(HEAD$,2,1))
6430 FOR I=1TO XE%:FORJ=1TOYE%
6440 INPUT#1,TEMP$
6450 EXEC ***** SWAP BALLS FOR SPACES *****
6460 IFLEN(SS$(I,J))<6THENFORII=1TO6-LEN(SS$(I,J)):SS$(I,J)=SS$(I,J)+" ":NEXT
6470 NEXT:NEXT
6480 CLOSE1
6490 END PROC *****
6500 :
6510 :
6520 :
6530 :
6540 PROC ***** SWAP SPACES FOR BALLS *****
6550 TEMP$="":TEMP%=0:PP$="":I%=0
6560 REPEAT
6570 TEMP%=TEMP%+1
6580 IF SS$(I,J)=""THEN I%=1:ELSE:PP$=MID$(SS$(I,J),TEMP%,1)
6590 IF I%=0THENIFPP$=" "THENTEMP$=TEMP$+""
6600 IF I%=0THENIFPP$<>" "THENTEMP$=TEMP$+PP$
6610 UNTIL I%=1 OR TEMP%=LEN(SS$(I,J))
6620 IF I%=1THENTEMP$=""
6630 END PROC *****
6640 :
6650 :
6660 :
6670 :
6680 PROC ***** SWAP BALLS FOR SPACES *****
6690 TEMP%=0:SS$(I,J)="":I%=0
6700 REPEAT
6710 TEMP%=TEMP%+1
6720 IF MID$(TEMP$,TEMP%,1)=""THENSS$(I,J)=SS$(I,J)+" ":ELSE:I%=1
6730 IFI%=1THENI%=0:SS$(I,J)=SS$(I,J)+MID$(TEMP$,TEMP%,1)
6740 UNTIL TEMP%=LEN(TEMP$)
6750 END PROC *****
6760 :
6770 :
6780 :
6790 :
6800 PROC ***** COPY SELECTED *****
6810 EXEC ***** CLEAR THE COMMENT BOX *****
6820 PRINTAT(2,1)"INPUT CELL TO COPY :-"
6830 PRINTAT(2,3)"{SPACE*6}ROW(1-96)"
6840 PRINTAT(2,2)"{SPACE*6}COL (A-L)"
6850 PRINTAT(18,2)"_"
6860 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
6870 UNTIL ASC(KB$)>64ANDASC(KB$)<77:PRINTAT(18,2)KB$;" " :CC%=ASC(KB$)-64
6880 PRINTAT(18,3)"__":TEMP$="":FOR I=1TO 2
6890 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
6900 UNTILASC(KB$)>47 AND ASC(KB$)<58:TEMP$=TEMP$+KB$
6910 IFI=1THENPRINTAT(18,3)KB$:ELSE:PRINTAT(19,3)KB$
6920 NEXTI:PRINTAT(20,3)" ":CR%=VAL(TEMP$)
6930 EXEC ***** CLEAR THE COMMENT BOX *****
6940 PRINTAT(2,1)"INPUT CELL(S) TO COPY IT TO :-"
6950 PRINTAT(1,2)"FROM: COL (A-L){SPACE*6}TO: COL (A-L)"
6960 PRINTAT(1,3)"{SPACE*6}ROW(1-96){SPACE*10}ROW(1-96)"
6970 PRINTAT(16,2)"_"
6980 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
6990 UNTILASC(KB$)>64ANDASC(KB$)<77:PRINTAT(16,2)KB$;" ":FC%=ASC(KB$)-64
7000 PRINTAT(16,3)"__"
7010 TEMP$="":FORI=1TO2
7020 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
7030 UNTILASC(KB$)>47 ANDASC(KB$)<58:TEMP$=TEMP$+KB$
7040 IFI=1THENPRINTAT(16,3)KB$:ELSE:PRINTAT(17,3)KB$
7050 NEXTI:PRINTAT(18,3)" ":FR%=VAL(TEMP$)
7060 PRINTAT(35,2)"_"
7070 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
7080 UNTILASC(KB$)>64ANDASC(KB$)<77 :PRINTAT(35,2)KB$;" ":TC%=ASC(KB$)-64
7090 PRINTAT(35,3)"__"
7100 TEMP$="":FORI=1TO2
7110 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
7120 UNTILASC(KB$)>47 ANDASC(KB$)<58:TEMP$=TEMP$+KB$
7130 IFI=1THENPRINTAT(35,3)KB$:ELSE:PRINTAT(36,3)KB$
7140 NEXTI:PRINTAT(37,3)" ":TR%=VAL(TEMP$)
7150 :
7160 EXEC ***** CLEAR THE COMMENT BOX *****
7170 PRINT AT(2,2)"(A)BSOLUTE OR (R)ELATIVE COPY ?"
7180 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
7190 UNTIL KB$="R" OR KB$="A"
7200 EXEC ***** CLEAR THE COMMENT BOX *****
7210 PRINTAT(2,1)"(A)BORT) OR (C)ONTINUE COPY :-"
7220 PRINT AT(2,2)"COPY CELL:- ";CHR$(CC%+64);CR%
7230 PRINTAT(2,3)"TO CELL(S):- ";CHR$(FC%+64);FR%;"TO ";CHR$(TC%+64);TR%
7240 IF KB$="R"THENTEMP$="(RELATIVE)":ELSE:TEMP$="(ABSOLUTE)"
7250 PRINTAT(28,2)TEMP$
7260 REPEAT:GETTEMP$:UNTILTEMP$="A"ORTEMP$="C":IFTEMP$="A"THEN KB$=""
7270 IF KB$="A"THEN EXEC ***** ABSOLUTE COPY *****
7280 IF KB$="R"THEN EXEC ***** RELATIVE COPY *****
7290 EXEC ***** CLEAR THE COMMENT BOX *****
7300 IFKB$="A"ORKB$="R"THENIFTEMP%=0THENEXEC ***** DISPLAY CELLS ON SCREEN *****
7310 END PROC *****
7320 :
7330 :
7340 :
7350 :
7360 PROC ***** ABSOLUTE COPY *****
7370 EXEC ***** CLEAR THE COMMENT BOX *****
7380 EXEC ***** PRINT 'WORKING' MESSAGE *****
7390 TEMP%=0:EXEC ***** CHECK COORDINATES INPUT *****
7400 IF TEMP%=0THENEXEC ***** ABSOLUTE *****
7410 IF TEMP%=1THENEXEC ***** CLEAR THE COMMENT BOX *****
7420 IF TEMP%=1THENPRINTAT(1,2)"*** INCORRECT ROW/COLUMN INPUT ***"
7430 IF TEMP%=1THENPRINTAT(1,3)"{SPACE*3}*** COPY ABORTED ***":FOR I=1TO1500:NEXTI
7440 END PROC
7450 :
7460 :
7470 :
7480 :
7490 PROC ***** CHECK COORDINATES INPUT *****
7500 IFCR%>96ORFR%>96ORTR%>96THENTEMP%=1
7510 IFCR%=0ORFR%=0ORTR%=0THENTEMP%=1
7520 IFFR%>TR%ORFC%>TC%THENTEMP%=1
7530 IF FC%<>TC% ANDFR%<>TR% THEN TEMP%=1
7540 IF KB$="R"AND LEFT$(SS$(CC%,CR%),1)<>"@"THENTEMP%=1
7550 IF FC%=TC%THENDA%=1:ELSE:DA%=0
7560 IF FC%=TC%ANDFR%=TR%THENTEMP%=1
7570 IFTEMP%=0ANDXE%<TC%THENXE%=TC%
7580 IFTEMP%=0ANDYE%<TR%THENYE%=TR%
7590 END PROC *****
7600 :
7610 :
7620 :
7630 :
7640 PROC ***** ABSOLUTE *****
7650 TEMP$=SS$(CC%,CR%)
7660 FOR I=FC%TOTC%:FORJ=FR%TOTR%
7670 SS$(I,J)=TEMP$
7680 NEXT:NEXT
7690 END PROC *****
7700 :
7710 :
7720 :
7730 :
7740 PROC ***** RELATIVE *****
7750 FI%=0:FORI=FC%TOTC%
7760 FORJ=FR%TOTR%
7770 TEMP$=SS$(CC%,CR%)
7780 P%=3:SS$(I,J)=LEFT$(TEMP$,2)
7790 REPEAT:I%=0
7800 F$=MID$(TEMP$,P%,1)
7810 FI%=ASC(F$):IFFI%<65ORFI%>76THENSS$(I,J)=SS$(I,J)+F$
7820 IFDA%=1THENIFF$>="A"ANDF$=<"L"THENSS$(I,J)=SS$(I,J)+F$
7830 IFDA%=1ANDF$>="A"ANDF$=<"L"THENP%=P%+2:I%=1
7840 IFI%=1THENPP$= STR$(VAL(MID$(TEMP$,P%-1,2))+FR%-CR%-(FR%-J))
7850 IFLEN(PP$)=2THENPP$=LEFT$(PP$,1)+"0"+RIGHT$(PP$,1)
7860 IFI%=1THENI%=0:SS$(I,J)=SS$(I,J)+RIGHT$(PP$,LEN(PP$)-1)
7870 IFDA%=0ANDF$>="A"ANDF$=<"L"THENI%=1
7880 IF DA%=0ANDASC(F$)>64ANDASC(F$)<77THENI%=1
7890 IFI%=1THENPP$=CHR$(ASC(MID$(TEMP$,P%,1))+FC%-CC%-(FC%-I))
7900 IFI%=1THENI%=0:SS$(I,J)=SS$(I,J)+PP$+MID$(TEMP$,P%+1,2):P%=P%+2
7910 IF F$="%"THENSS$(I,J)=SS$(I,J)+MID$(TEMP$,P%+1,2):P%=P%+2
7920 P%=P%+1
7930 UNTIL P%>LEN(TEMP$)
7940 NEXT:NEXT
7950 END PROC
7960 :
7970 :
7980 :
7990 :
8000 PROC ***** RELATIVE COPY *****
8010 EXEC ***** CLEAR THE COMMENT BOX *****
8020 EXEC ***** PRINT 'WORKING' MESSAGE *****
8030 TEMP%=0:EXEC ***** CHECK COORDINATES INPUT *****
8040 IF TEMP%=0THENEXEC ***** RELATIVE *****
8050 IF TEMP%=1THENEXEC ***** CLEAR THE COMMENT BOX *****
8060 IF TEMP%=1THENPRINTAT(1,2)"*** INCORRECT ROW/COLUMN INPUT ***"
8070 IF TEMP%=1THENPRINTAT(1,3)"{SPACE*3}*** COPY ABORTED ***":FOR I=1TO1500:NEXTI
8080 END PROC
8090 :
8100 :
8110 :
8120 :
8130 PROC ***** CALC SELECTED *****
8140 EXEC ***** PRINT 'WORKING' MESSAGE *****
8150 FORJ=1TOYE%:FORI=1TOXE%
8160 IF LEFT$(SS$(I,J),1)="@"THEN EXEC ***** VALIDATE FORMULA *****
8170 IF LEFT$(SS$(I,J),1)="@"AND FI%=0THEN EXEC ***** CALCULATE *****
8180 NEXT I,J
8190 EXEC ***** DISPLAY CELLS ON SCREEN *****
8200 EXEC ***** CLEAR THE COMMENT BOX *****
8210 END PROC *****
8220 :
8230 :
8240 :
8250 :
8260 :
8270 PROC ***** VALIDATE FORMULA *****
8280 FI%=3:P%=2:TEMP$=SS$(I,J)
8290 REPEAT:PP$=MID$(TEMP$,P%,1)
8300 IFPP$<>"+"ANDPP$<>"-"ANDPP$<>"*"ANDPP$<>"/"ANDPP$<>""THENFI%=1
8310 P%=P%+1
8320 I%=0:PP$=MID$(TEMP$,P%,1)
8330 IFASC(PP$)<65ANDASC(PP$)>76THENI%=I%+1
8340 IFASC(PP$)<48ANDASC(PP$)>57THENI%=I%+1
8350 IFPP$<>"%"THENI%=I%+1
8360 IFPP$<>"S"THENI%=I%+1
8370 IF I%=4THENFI%=1:I%=0
8380 FF%=ASC(PP$)
8390 IFFF%>64ANDFF%<77THENFI%=0: EXEC ** CO-ORD **
8400 IFFF%>47ANDFF%<58THENFI%=0: EXEC ** NUMBER **
8410 IFFF%=37THENFI%=0:EXEC ** PERCENT **
8420 IFFF%=83THENFI%=0:EXEC ** SUM **
8430 UNTILP%>=LEN(TEMP$)ORFI%=1
8440 PP$=RIGHT$(TEMP$,1):IFASC(PP$)<48ORASC(PP$)>57THENFI%=1
8450 END PROC *****
8460 :
8470 PROC ** CO-ORD **
8480 IFLEN(TEMP$)<P%+2THENFI%=1:TEMP$=TEMP$+"{SPACE*2}"
8490 FORII=1TO2
8500 IFASC(MID$(TEMP$,P%+II,1))<48ORASC(MID$(TEMP$,P%+II,1))>57THENFI%=1
8510 NEXT II
8520 IF FI%=0 THENP%=P%+3
8530 END PROC **
8540 :
8550 PROC ** NUMBER **
8560 REPEAT
8570 P%=P%+1
8580 IF MID$(TEMP$,P%,1)="."THENP%=P%+1
8590 II=0:IFP%>LEN(TEMP$)THENII=1:P%=P%-1
8600 UNTIL II=1ORASC(MID$(TEMP$,P%,1))<48 ORASC(MID$(TEMP$,P%,1))>57
8610 IF II=1THENP%=P%+1
8620 END PROC **
8630 :
8640 PROC ** PERCENT **
8650 FORII=1TO2
8660 IF ASC(MID$(TEMP$,P%+II,1))<48ORASC(MID$(TEMP$,P%+II,1))>57THENFI%=1
8670 NEXTII
8680 IF FI%=0THEN P%=P%+3
8690 END PROC **
8700 :
8710 PROC ** SUM **
8720 REPEAT
8730 IFLEN(TEMP$)-P%<6THENTEMP$=TEMP$+" "
8740 UNTIL LEN(TEMP$)-P%>=6
8750 FORII=0TO1
8760 IFASC(MID$(TEMP$,P%+1+(II*3),1))<65ORASC(MID$(TEMP$,P%+1+(II*3),1))>76THENFI%=1
8770 NEXT II:IFFI%=1 THEN STOP
8780 FORII=0TO1:FORJJ=2TO3
8790 IFASC(MID$(TEMP$,P%+JJ+(II*3),1))<48ORASC(MID$(TEMP$,P%+JJ+(II*3),1))>57THENFI%=1
8800 NEXTJJ,II:IF FI%=0THENP%=P%+7
8810 END PROC **
8820 :
8830 :
8840 :
8850 :
8860 :
8870 PROC ***** CALCULATE *****
8880 P%=0:G%=0:REPEAT:P%=P%+1:IFMID$(SS$(I,J),P%,1)="@"THENG%=G%+1
8890 UNTILP%=LEN(SS$(I,J)):IFG%=>2THENEXEC * CUT OFF END *
8900 P%=2:SO=0:TL=0
8910 REPEAT
8920 PP$=MID$(SS$(I,J),P%,1)
8930 P%=P%+1
8940 BB=ASC(MID$(TEMP$,P%,1))
8950 IFBB>64ANDBB<77THEN EXEC CO-ORD
8960 IFBB>47ANDBB<58THEN EXEC NUMBER
8970 IFBB=37THEN EXEC PERCENT
8980 IFBB=83THEN EXEC SUM
8990 IFPP$="+"THENSO=SO+TL
9000 IFPP$="-"THENSO=SO-TL
9010 IFPP$="*"THENSO=SO*TL
9020 IFPP$=""THENSO=SO^TL
9030 IFPP$="/"ANDTL<>0THEN SO=SO/TL
9040 IFPP$="/"ANDTL=0THENSO=0
9050 UNTIL P%=LEN (TEMP$)+1ORFI%=1ORMID$(TEMP$,P%,1)="@"
9060 IFSO=>0THENSS$(I,J)=SS$(I,J)+"@"+LEFT$(RIGHT$(STR$(SO),LEN(STR$(SO))-1),10)
9070 IFSO<0THENSS$(I,J)=SS$(I,J)+"@"+LEFT$(STR$(SO),10)
9080 END PROC *****
9090 :
9100 PROC CO-ORD
9110 TL=0:IFVAL(MID$(TEMP$,P%+1,2))>96THENFI%=1
9120 IF FI%=0THENJY=VAL(MID$(TEMP$,P%+1,2)):ELSE:JY=1
9130 IY=ASC(MID$(TEMP$,P%,1))-64
9140 PM$=SS$(IY,JY)
9150 I%=1:REPEAT:I%=I%+1:UNTILMID$(PM$,I%,1)="@"ORI%>=LEN(PM$)
9160 IFI%>=LEN(PM$)THENFI%=1:ELSE:TL=VAL(MID$(PM$,I%+1,10))
9170 P%=P%+3
9180 END PROC ***
9190 :
9200 :
9210 :
9220 :
9230 :
9240 PROC * CUT OFF END *
9250 G%=LEN(SS$(I,J))+1:REPEAT
9260 G%=G%-1
9270 UNTILMID$(SS$(I,J),G%,1)="@"
9280 SS$(I,J)=LEFT$(SS$(I,J),G%-1)
9290 END PROC *
9300 :
9310 :
9320 :
9330 :
9340 :
9350 PROC NUMBER
9360 PM$="":XS%=0:TL=0: P%=P%-1:  REPEAT
9370 P%=P%+1:IF MID$(TEMP$,P%,1)="."THENP%=P%+1:PM$=PM$+"."
9380 PM$=PM$+MID$(TEMP$,P%,1)
9390 IFP%=>LEN(TEMP$)THENP%=P%-1:XS%=1
9400 IZ%=ASC(MID$(TEMP$,P%+1,1)):IFIZ%=46THENIZ%=48
9410 UNTILIZ%<48ORIZ%>57ORXS%=1
9420 IF XS%=1THEN P%=P%+1
9430 TL=VAL(LEFT$(PM$,10)):P%=P%+1
9440 END PROC
9450 :
9460 :
9470 :
9480 :
9490 :
9500 PROC PERCENT
9510 TL=VAL(MID$(TEMP$,P%+1,2))/100
9520 END PROC
9530 :
9540 :
9550 :
9560 :
9570 :
9580 :
9590 PROC SUM
9600 PM$="":TL=0:FORII=0TO1:IFVAL(MID$(TEMP$,P%+2+(3*II),2))>96THENFI%=1
9610 NEXTII
9620 FORJJ=VAL(MID$(TEMP$,P%+2,2))TOVAL(MID$(TEMP$,P%+5,2))
9630 FORII=ASC(MID$(TEMP$,P%+1,1))-64TOASC(MID$(TEMP$,P%+4,1))-64
9640 PM$=SS$(II,JJ)
9650 I%=1:REPEAT:I%=I%+1:UNTILMID$(PM$,I%,1)="@"ORI%>=LEN(PM$)
9660 IFI%=LEN(PM$)THENFI%=1:ELSE:TL=TL+VAL(MID$(PM$,I%+1,10))
9670 NEXT II,JJ
9680 P%=P%+7
9690 END PROC
9700 :
9710 :
9720 :
9730 PROC ***** GOTO SELECTED *****
9740 EXEC ***** CLEAR THE COMMENT BOX *****
9750 PRINTAT(2,1)"INPUT CELL TO GOTO":PRINTAT(2,2)"{SPACE*3}COL (A-L)"
9760 PRINTAT(2,3)"{SPACE*3}ROW(1-96)"AT(16,2)"_"
9770 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
9780 UNTIL ASC(KB$)>64ANDASC(KB$)<77:PRINTAT(16,2)KB$;" ":GC%=ASC(KB$)-64
9790 PRINTAT(16,3)"__":TEMP$="":FORI=1TO2
9800 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
9810 UNTILASC(KB$)>47ANDASC(KB$)<58:TEMP$=TEMP$+KB$
9820 IFI=1THENPRINTAT(16,3)KB$:ELSE:PRINTAT(17,3)KB$
9830 NEXTI:PRINTAT(18,3)" ":GR%=VAL(TEMP$)
9840 PRINTAT(21,1)"{SPACE*2}GOTO CELL:- ";CHR$(GC%+64);GR%;AT(23,2)"(A)BORT OR"
9850 PRINTAT(23,3)"(C)ONTINUE"
9860 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
9870 UNTILKB$="A"ORKB$="C"
9880 IFGR%>96ORGR%=0THENKB$="A"
9890 IF KB$="C"THEN EXEC ** GOTO **
9900 EXEC ***** CLEAR THE COMMENT BOX *****
9910 END PROC *****
9920 :
9930 :
9940 :
9950 PROC ** GOTO **
9960 YY%=GR%+1:REPEAT:IFYY%/6<>INT(YY%/6)ORYY%>78THENYY%=YY%-1
9970 UNTILYY%/6=INT(YY%/6)ANDYY%<79
9980 XX%=GC%-1:REPEAT:IFXX%/3<>INT(XX%/3)ORXX%>5THENXX%=XX%-1
9990 UNTILXX%/3=INT(XX%/3)ANDXX%<7
10000 XX%=XX%+1:YY%=YY%+1:CX%=GC%-XX%+1:CY%=GR%-YY%+1
10010 EXEC ***** PRINT 'WORKING' MESSAGE *****
10020 EXEC ***** PRINT NUMBERS DOWN THE SIDE *****
10030 EXEC ***** PRINT LETTERS ACROSS THE TOP *****
10040 EXEC ***** DISPLAY CELLS ON SCREEN *****
10050 END PROC *****
10060 :
10070 :
10080 :
10090 PROC ***** FORM SELECTED *****
10100 EXEC ***** CLEAR THE COMMENT BOX *****
10110 PRINTAT(2,1)"INPUT SIGNIFICANT FIGURES (1-6) _"
10120 PRINTAT(2,2)"INPUT BACKGROUND COLOUR (1-8) "
10130 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
10140 UNTILASC(KB$)>48ANDASC(KB$)<55:PRINTAT(34,1)KB$ " {down}{LEFT*4}_":DP%=VAL(KB$)
10150 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
10160 UNTILASC(KB$)>48ANDASC(KB$)<57:PRINTAT(34,2)KB$" {down}{LEFT*4}_"
10170 COLOURASC(KB$)-1,ASC(KB$)-1
10180 EXEC ***** CLEAR THE COMMENT BOX *****
10190 END PROC
10200 :
10210 :
10220 :
10230 :
10240 :
10250 PROC ***** HELP SELECTED *****
10260 EXEC ***** CLEAR THE COMMENT BOX *****
10270 PRINTAT(1,1)"<F1> MOVES HIGHLIGHT, <F7> TO END HELP"
10280 REPEAT
10290 REPEAT:EXEC ***** INPUT A KEY OR COMMAND *****
10300 UNTILKB$="{f1}"ORKB$="{f7}"
10310 IFKB$="{f1}"THENEXEC ***** MOVE HIGHLIGHT ALONG *****
10320 IFKB$="{f1}"THEN EXEC F1
10330 UNTIL KB$="{f7}":KB$=""
10335 EXEC ***** CLEAR THE COMMENT BOX *****
10340 END PROC *****
10350 :
10360 PROC F1
10370 IFHI%=1THENM$="COPY A CELL INTO DIFFERENT LOCATION(S)"
10380 IFHI%=2THENM$="LOAD/SAVE/ERASE/ERRORS FROM DISC DRIVE"
10390 IFHI%=3THENM$="MOVE CURSOR TO ANY CELL ON THE SHEET{SPACE*2}"
10400 IFHI%=4THENM$="RECALCULATE ALL FORMULAE ON SHEET{SPACE*5}"
10410 IFHI%=5THENM$="PRINT OUT THE SHEET ON THE PRINTER{SPACE*4}"
10420 IFHI%=6THENM$="PRINTS BRIEF DESCRIPTION OF OPTIONS{SPACE*3}"
10430 IFHI%=7THENM$="QUIT THE PROGRAM OR CLEAR THE SHEET{SPACE*3}"
10440 IFHI%=8THENM$="FORMAT OF SIGNIFICANT FIGURES & COLOUR"
10450 PRINTAT(1,2)M$
10460 END PROC
